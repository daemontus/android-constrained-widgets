buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'android-maven'
apply plugin: 'signing'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 24
        versionCode getAppVersionCode()
        versionName getAppVersionName()
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
}

group = "com.marvinlabs"
version = getAppVersionName()

android.libraryVariants.all { variant ->
    def name = variant.buildType.name

    if (name.equalsIgnoreCase("debug")) {
        return; // Skip debug builds.
    }
    task("javadoc${variant.name.capitalize()}", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        //ext.androidJar = files(plugins.findPlugin("com.android.library").getBootClasspath())
        //classpath = files(variant.javaCompile.classpath.files) + ext.androidJar
        exclude '**/internal/**'
        failOnError false
    }

    task("bundleJavadoc${variant.name.capitalize()}", type: Jar) {
        description "Bundles Javadoc into zip for $variant.name."
        classifier = "javadoc"
        from tasks["javadoc${variant.name.capitalize()}"]
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java
}

task jar(type: Jar) {
    from 'build/classes/release'
}

task javadocJar(type: Jar, dependsOn: "javadocRelease") {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives jar
}

task wrapper(type: Wrapper) {
    description "Generates the gradle wrapper"
    gradleVersion = "2.14"
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: getSonatypeRepositoryUrl()) {
                authentication(userName: getSonatypeRepositoryUser(), password: getSonatypeRepositoryPassword())
            }

            pom.project {
                name 'Android Constrained Widgets'
                artifactId 'android-constrained-widgets'
                packaging 'aar'
                description 'A set of aspect-ratio constrained widgets for Android'
                url 'https://github.com/marvinlabs/android-constrained-widgets'

                scm {
                    url 'scm:git@github.com:marvinlabs/android-constrained-widgets.git'
                    connection 'scm:git@github.com:marvinlabs/android-constrained-widgets.git'
                    developerConnection 'scm:git@github.com:marvinlabs/android-constrained-widgets.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'vpratfr'
                        name 'Vincent Mimoun-Prat'
                    }
                }
            }
        }
    }
}
